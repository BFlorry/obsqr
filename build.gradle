buildscript {
	repositories {
		mavenCentral()
	}
	dependencies {
		classpath 'com.android.tools.build:gradle:1.0.+'
	}
}

apply plugin: 'android'

repositories {
	mavenCentral()
}

dependencies {
	compile 'co.trikita:knork:1.3.0+'
}

android {
	compileSdkVersion 'android-21'
	buildToolsVersion '21.1.2'

	defaultConfig {
		minSdkVersion 9
		targetSdkVersion 19
		versionCode 14
		versionName "2.8"
		ndk {
			abiFilters "armeabi", "armeabi-v7a", "x86", "mips"
			moduleName "zbar"
			ldLibs "log"
			cFlags "-std=c99"
		}
	}

	signingConfigs {
		release
	}

	buildTypes {
		release {
			minifyEnabled false
			signingConfig signingConfigs.release
			applicationVariants.all { variant ->
				variant.outputs.each { output ->
					output.outputFile = new File(output.outputFile.parentFile,
							output.outputFile.name.replace(".apk", "-${variant.versionName}.apk"));
				}
			}
		}
	}

	productFlavors {
		full {
			buildConfigField "boolean", "HAS_WIFI_PERMISSIONS", "true"
		}
		noperm {
			buildConfigField "boolean", "HAS_WIFI_PERMISSIONS", "false"
		}
	}

	lintOptions {
		abortOnError false
	}
}

if (project.hasProperty('storeFile') &&
		project.hasProperty('storePassword') &&
		project.hasProperty('keyAlias') &&
		project.hasProperty('keyPassword')) {
	android.signingConfigs.release.storeFile = file(storeFile)
	android.signingConfigs.release.storePassword = storePassword
	android.signingConfigs.release.keyAlias = keyAlias
	android.signingConfigs.release.keyPassword = keyPassword
} else {
	buildTypes.release.signingConfig = null
}

task run(type: Exec, dependsOn: ':installFullDebug') {
	commandLine 'adb', 'shell', 'am', 'start', '-n', 'trikita.obsqr/.ObsqrActivity'
}

task runNoPerm(type: Exec, dependsOn: ':installNopermDebug') {
	commandLine 'adb', 'shell', 'am', 'start', '-n', 'trikita.obsqr/.ObsqrActivity'
}


